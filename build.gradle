plugins {
    id 'java'
    id 'pmd'
    id 'jacoco'
}

group 'ru.javani'
version '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'junit:junit:4.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation("org.mockito:mockito-core:3.12.4")
    testImplementation("org.mockito:mockito-junit-jupiter:4.0.0")
}

test {
    useJUnitPlatform()
    dependsOn pmdMain, pmdTest
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

pmd {
    consoleOutput = true
    toolVersion = "6.54.0"
    ruleSetFiles = files("config/pmd.xml") // Исключения только через внешний файл
    ruleSets = []
}

jacocoTestReport {
    reports {
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "ru/javani/AppMain.*",
                    "ru/javani/CSVWriter.*"
            ])
        }))
    }
}
jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                value = 'MISSEDCOUNT'
                maximum = 0
            }
        }
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "ru/javani/AppMain.*",
                    "ru/javani/CSVWriter.*"
            ])
        }))
    }
}
check.dependsOn jacocoTestReport, jacocoTestCoverageVerification